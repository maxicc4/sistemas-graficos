<!doctype html>
<html lang="es-AR">
    <meta charset="UTF-8"/>
    <head>
        <title></title>
        <link rel="stylesheet" href="style.css">
    </head>
    <body>
        <canvas id="my-canvas">
            Your browser does not support the HTML5 canvas element.
        </canvas>

        <script type="text/javascript" src="vendor/js/gl-matrix.js"></script>
        <script type="text/javascript" src="vendor/js/jquery-3.5.1.js"></script>
        <script type="text/javascript" src="js/webgl.js"></script>
        <script type="text/javascript" src="js/grid3D.js"></script>
        <script type="text/javascript" src="js/commonSurfaces.js"></script>
        <script type="text/javascript" src="js/helicopterSurfaces.js"></script>
        <script type="text/javascript" src="js/object3d.js"></script>
        <script type="text/javascript" src="js/cameras.js"></script>
        <script type="text/javascript" src="js/cameraController.js"></script>
        <script type="text/javascript" src="js/helicopterController.js"></script>
        <script type="text/javascript" src="js/texture.js"></script>


        <script type="x-shader/x-vertex" id="vertex-shader">
            precision highp float;

            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aUV;

            uniform mat4 modelMatrix;
            uniform mat4 viewMatrix;
            uniform mat4 projMatrix;

            uniform mat4 normalMatrix;

            uniform vec3 uCameraPosition;

            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUV;
            varying vec3 vToCamera;

            void main(void) {
                gl_Position = projMatrix * viewMatrix * modelMatrix * vec4(aVertexPosition, 1.0);

                vPosWorld = (modelMatrix*vec4(aVertexPosition,1.0)).xyz;    //la posicion en coordenadas de mundo
                vNormal = (normalMatrix*vec4(aVertexNormal,1.0)).xyz;       //la normal en coordenadas de mundo
                vUV = aUV;
                vToCamera = normalize(uCameraPosition - vPosWorld);
            }
        </script>

        <script type="x-shader/x-vertex" id="vertex-shader-terrain">
            precision highp float;

            attribute vec3 aVertexPosition;
            attribute vec2 aUV;

            uniform mat4 modelMatrix;
            uniform mat4 viewMatrix;
            uniform mat4 projMatrix;

            uniform mat4 normalMatrix;

            uniform vec3 uCameraPosition;

            uniform vec2 uOffsetUV;
            uniform float uScaleUV;

            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUV;
            varying vec3 vToCamera;

            uniform sampler2D uSampler0;

            const float epsilon=0.000000001;
            const float amplitud=40.0;

            void main(void) {
                vec3 position = aVertexPosition;
                vec2 uv = uOffsetUV + aUV*uScaleUV;

                vec4 center = texture2D(uSampler0, vec2(uv.s, uv.t));
                vec4 masU = texture2D(uSampler0, vec2(uv.s+epsilon, uv.t));
                vec4 masV = texture2D(uSampler0, vec2(uv.s, uv.t+epsilon));

                vec4 menosU = texture2D(uSampler0, vec2(uv.s-epsilon, uv.t));
                vec4 menosV = texture2D(uSampler0, vec2(uv.s, uv.t-epsilon));


                // elevamos la coordenada Y
                position.y+=center.x*amplitud;

                vec4 worldPos = modelMatrix*vec4(position, 1.0);

                gl_Position = projMatrix*viewMatrix*worldPos;

                vPosWorld=worldPos.xyz;

                /*
                 hay que calcular la normal ya que el valor original es la normal del plano
                 pero luego de elevar Y, el valor original no tiene sentido

                 La idea es calcular la diferencia de altura entre 2 muestras proximas
                 y estimar el vector tangente.

                 Haciendo lo mismo en el eje U y en el eje V tenemos 2 vectores tangentes a la superficie
                 Luego calculamos el producto vectorial y obtenemos la normal

                 Para tener un resultado con mayor precision, para cada eje U y V calculo 2 tangentes
                 y las promedio
                */
                float angU=atan((masU.x-center.x)*amplitud,epsilon);
                float angV=atan((masV.x-center.x)*amplitud,epsilon);

                // tangentes en U y en V
                vec3 gradU1=vec3(cos(angU),sin(angU),0.0);
                vec3 gradV1=vec3(0.0      ,sin(angV),cos(angV));

                angU=atan((center.x-menosU.x)*amplitud,epsilon);
                angV=atan((center.x-menosV.x)*amplitud,epsilon);

                // segundo conjunto de tangentes en U y en V
                vec3 gradU2=vec3(cos(angU),sin(angU),0.0);
                vec3 gradV2=vec3(0.0      ,sin(angV),cos(angV));



                // calculo el producto vectorial
                vec3 tan1=(gradV1+gradV2)/2.0;
                vec3 tan2=(gradU1+gradU2)/2.0;
                vNormal=cross(tan1,tan2);
                vUV=uv;
                vToCamera = normalize(uCameraPosition - vPosWorld);
            }
        </script>

        <script type="x-shader/x-fragment" id="fragment-shader">
            precision highp float;
            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUV;
            varying vec3 vToCamera;

            uniform sampler2D uSampler0;

            uniform bool uUseLighting;          // usar iluminacion si/no

            void main(void) {
                vec3 color=texture2D(uSampler0, vec2(vUV.s, vUV.t)).xyz;
                if (uUseLighting) {
                    vec3 L = normalize(vec3(0.0177,0.707,0.707));
                    vec3 N = vNormal;
                    vec3 R = reflect(L,N);
                    vec3 V = vToCamera;

                    float Iamb = 0.3;
                    float Idif = clamp(dot(N,L),0.0,1.0);
                    float Ispe = 0.5*pow(clamp(dot(V,R),0.0,1.0), 2.0);

                    color=( Iamb + Idif + Ispe )*color;
                }

                gl_FragColor = vec4(color,1.0);
            }
        </script>

        <script type="x-shader/x-fragment" id="fragment-shader-terrain">
            precision highp float;
            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUV;
            varying vec3 vToCamera;

            uniform sampler2D uSampler0;
            uniform sampler2D uSampler1;
            uniform sampler2D uSampler2;
            uniform sampler2D uSampler3;
            uniform sampler2D uSampler4;
            uniform sampler2D uSampler5;
            uniform sampler2D uSampler6;
            uniform sampler2D uSampler7;

            uniform bool uUseLighting;          // usar iluminacion si/no

            //--------------------------------------------------------------------------------------

            /* https://www.shadertoy.com/view/XsX3zB
             *
             * The MIT License
             * Copyright Â© 2013 Nikita Miropolskiy
             *
             * ( license has been changed from CCA-NC-SA 3.0 to MIT
             *
             *   but thanks for attributing your source code when deriving from this sample
             *   with a following link: https://www.shadertoy.com/view/XsX3zB )
             *
             * ~
             * ~ if you're looking for procedural noise implementation examples you might
             * ~ also want to look at the following shaders:
             * ~
             * ~ Noise Lab shader by candycat: https://www.shadertoy.com/view/4sc3z2
             * ~
             * ~ Noise shaders by iq:
             * ~     Value    Noise 2D, Derivatives: https://www.shadertoy.com/view/4dXBRH
             * ~     Gradient Noise 2D, Derivatives: https://www.shadertoy.com/view/XdXBRH
             * ~     Value    Noise 3D, Derivatives: https://www.shadertoy.com/view/XsXfRH
             * ~     Gradient Noise 3D, Derivatives: https://www.shadertoy.com/view/4dffRH
             * ~     Value    Noise 2D             : https://www.shadertoy.com/view/lsf3WH
             * ~     Value    Noise 3D             : https://www.shadertoy.com/view/4sfGzS
             * ~     Gradient Noise 2D             : https://www.shadertoy.com/view/XdXGW8
             * ~     Gradient Noise 3D             : https://www.shadertoy.com/view/Xsl3Dl
             * ~     Simplex  Noise 2D             : https://www.shadertoy.com/view/Msf3WH
             * ~     Voronoise: https://www.shadertoy.com/view/Xd23Dh
             * ~
             *
             */

            /* discontinuous pseudorandom uniformly distributed in [-0.5, +0.5]^3 */
            vec3 random3(vec3 c) {
                float j = 4096.0*sin(dot(c,vec3(17.0, 59.4, 15.0)));
                vec3 r;
                r.z = fract(512.0*j);
                j *= .125;
                r.x = fract(512.0*j);
                j *= .125;
                r.y = fract(512.0*j);
                return r-0.5;
            }

            /* skew constants for 3d simplex functions */
            const float F3 =  0.3333333;
            const float G3 =  0.1666667;

            /* 3d simplex noise */
            float noise(vec3 p) {
                /* 1. find current tetrahedron T and it's four vertices */
                /* s, s+i1, s+i2, s+1.0 - absolute skewed (integer) coordinates of T vertices */
                /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/

                /* calculate s and x */
                vec3 s = floor(p + dot(p, vec3(F3)));
                vec3 x = p - s + dot(s, vec3(G3));

                /* calculate i1 and i2 */
                vec3 e = step(vec3(0.0), x - x.yzx);
                vec3 i1 = e*(1.0 - e.zxy);
                vec3 i2 = 1.0 - e.zxy*(1.0 - e);

                /* x1, x2, x3 */
                vec3 x1 = x - i1 + G3;
                vec3 x2 = x - i2 + 2.0*G3;
                vec3 x3 = x - 1.0 + 3.0*G3;

                /* 2. find four surflets and store them in d */
                vec4 w, d;

                /* calculate surflet weights */
                w.x = dot(x, x);
                w.y = dot(x1, x1);
                w.z = dot(x2, x2);
                w.w = dot(x3, x3);

                /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
                w = max(0.6 - w, 0.0);

                /* calculate surflet components */
                d.x = dot(random3(s), x);
                d.y = dot(random3(s + i1), x1);
                d.z = dot(random3(s + i2), x2);
                d.w = dot(random3(s + 1.0), x3);

                /* multiply d by w^4 */
                w *= w;
                w *= w;
                d *= w;

                /* 3. return the sum of the four surflets */
                return dot(d, vec4(52.0));
            }

            //--------------------------------------------------------------------------------------

            void main(void) {
                // Low zone (arena)
                vec3 sand1=texture2D(uSampler1,vUV*100.0).xyz;
                vec3 sand2=texture2D(uSampler1,vUV*50.0).xyz;
                vec3 sand3=texture2D(uSampler1,vUV*2.11).xyz;

                vec3 lowZone=mix(mix(sand1,sand2,0.5),sand3,0.3);

                // Medium zone (pasto y tierra)
                vec3 pasture1=texture2D(uSampler2,vUV*100.0).xyz;
                vec3 pasture2=texture2D(uSampler2,vUV*50.0).xyz;
                vec3 pasture3=texture2D(uSampler2,vUV*2.11).xyz;

                vec3 land1=texture2D(uSampler3,vUV*100.0).xyz;
                vec3 land2=texture2D(uSampler4,vUV*50.0).xyz;
                vec3 land3=texture2D(uSampler5,vUV*50.0).xyz;

                vec3 stone=texture2D(uSampler6,vUV*100.0).xyz;

                vec3 pastureColor=mix(mix(pasture1,pasture2,0.5),pasture3,0.3);

                vec3 landColor=mix(mix(land1,land2,0.7),land3,0.3);

                float noise4=noise(vUV.xyx*10.59);
                float noise5=noise(vUV.xyx*20.25);
                float noise6=noise(vUV.xyx*200.03);

                float mask2=mix(mix(noise4,noise5,0.5),noise6,0.3);
                mask2=smoothstep(-0.1,0.5,mask2);

                vec3 mediumZone=mix(pastureColor,landColor,mask2);

                // High zone (musgo)
                vec3 moss1=texture2D(uSampler7,vUV*50.0).xyz;
                vec3 moss2=texture2D(uSampler7,vUV*25.0).xyz;
                vec3 moss3=texture2D(uSampler7,vUV*2.11).xyz;

                float noise7=noise(vUV.xyx*200.59);
                float noise8=noise(vUV.xyx*400.25);
                float noise9=noise(vUV.xyx*800.03);

                float mask3=mix(mix(noise7,noise8,0.5),noise9,0.3);
                mask3=smoothstep(-0.1,0.5,mask3);

                vec3 highZone=mix(mix(moss1,moss2,0.5),moss3,0.3);
                highZone = mix(highZone,stone,mask3);

                float mixLowMedium=smoothstep(0.0,8.0,vPosWorld.y);
                float mixMediumHigh=smoothstep(10.0,20.0,vPosWorld.y);
                vec3 color = mix(mix(lowZone,mediumZone,mixLowMedium),highZone,mixMediumHigh);

                vec3 L = normalize(vec3(0.0177,0.707,0.707));
                vec3 N = vNormal;
                vec3 R = reflect(L,N);
                vec3 V = vToCamera;

                float Iamb = 0.3;
                float Idif = clamp(dot(N,L),0.0,1.0);
                float Ispe = 0.5*pow(clamp(dot(V,R),0.0,1.0), 2.0);

                color=( Iamb + Idif + Ispe )*color;

                gl_FragColor = vec4(color,1.0);
            }
        </script>

        <script type="x-shader/x-fragment" id="fragment-shader-water">
            precision highp float;
            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUV;
            varying vec3 vToCamera;

            uniform sampler2D uSampler0;
            uniform vec2 uOffsetUV;

            uniform bool uUseLighting;          // usar iluminacion si/no

            void main(void) {
                vec2 uvWater = vUV + uOffsetUV;
                vec3 water1=texture2D(uSampler0,uvWater*100.0).xyz;
                vec3 water2=texture2D(uSampler0,uvWater*50.0).xyz;
                vec3 water3=texture2D(uSampler0,uvWater*2.11).xyz;

                vec3 color=mix(mix(water1,water2,0.5),water3,0.3);

                vec3 L = normalize(vec3(0.0177,0.707,0.707));
                vec3 N = vNormal;
                vec3 R = reflect(L,N);
                vec3 V = vToCamera;

                float Iamb = 0.3;
                float Idif = clamp(dot(N,L),0.0,1.0);
                float Ispe = 0.5*pow(clamp(dot(V,R),0.0,1.0), 2.0);

                color=( Iamb + Idif + Ispe )*color;

                gl_FragColor = vec4(color,1.0);
            }
        </script>

        <script>
            $(document).ready(function(){
                initWebGL();
            });
        </script>


    </body>
</html>
